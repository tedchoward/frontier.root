FrontierVcsFile:3:scpt:system.menus.agent

«Changes:
	«12/2/01; 7:32:31 PM by JES
		«If user.radio.settings.flExpired is defined and true, do nothing.
	«1/28/01; 10:43:51 AM by DW
		«We found a performance bottleneck in Radio. When it's enabling and disabling commands in the File menu, it's calling window.frontmost (apparently) six times per second. This is a fairly slow verb (it can be optimized). So we cache the value in system.temp.windowFrontmost. This is then used by pike.isFileMenuItemEnabled.
		«Also noted that it's called a few times on every keystroke.
	«10/11/00; 10:25:30 PM by PBS
		«If Radio UserLand, add or remove Outliner and HTML menus.
	«Some time in 1991 DW
		«Add or remove menus depending on frontmost window

if defined (user.radio.settings.flExpired) {
	if user.radio.settings.flExpired {
		return (true)}};

system.temp.windowFrontmost = window.frontmost (); //cache it
local (adr = system.temp.windowFrontmost);
local (typefront = window.getType (adr));
if typefront == system.menus.data.currentMenuType { «no change, nothing to do
	return (true)};

bundle { //handle Outliner and HTML menus
	if system.environment.isPike {
		menu.remove (@system.menus.menubar); //Outliner
		menu.remove (@html.menu.theMenu);
		case typefront {
			outlineType {
				menu.install (@system.menus.menubar); //Outliner menu
				menu.install (@html.menu.theMenu)};
			scriptType;
			tableType;
			menubarType {
				menu.install (@system.menus.menubar)}}}}; //Outliner menu

local (adrold = @system.menus.modals.[system.menus.data.currentMenuType]);
if defined (adrold^) {
	menu.remove (adrold)};

local (adrnew = @system.menus.modals.[typefront]);
if defined (adrnew^) {
	menu.install (adrnew)};

system.menus.data.currentMenuType = typefront;
return (true)


