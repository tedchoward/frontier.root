FrontierVcsFile:3:scpt:system.verbs.apps.weblogUpdates.ping

on ping (name, url, changesUrl=url, categoryName="none", adrdata=@weblogUpdates.data, adrmessage=nil) {
	«Changes
		«1/21/02; 12:27:18 PM by DW
			«Ooops, I released a version with fldebug true. Sorry. :->
		«1/21/02; 10:53:51 AM by DW
			«New optional param adrmessage.
			«Now return true if the ping got through, false otherwise.
		«11/28/01; 12:37:58 PM by DW
			«Add optional parameters, changesUrl and categoryName
	try {
		local (params = {"weblogname":name, "weblogurl":url, "changesurl":changesUrl, "categoryname":categoryname});
		with adrdata^ {
			local (response = xml.rpc (server, port, "weblogUpdates.ping", @params, rpcPath:rpcPath, fldebug:false, flShowMessages:flShowMessages, protocol:protocol, soapAction:soapAction, flAsynch:false));
			if adrmessage != nil {
				adrmessage^ = response.message};
			return (true)}}
	else {
		if adrmessage != nil {
			adrmessage^ = tryError};
		return (false)}}
«bundle //test code
	«ping ("Scripting News in XML", "http://scriptingnews.userland.com/xml/scriptingNews2.xml", categoryname:"rss")
	«ping ("Scripting News", "http://www.scripting.com/", categoryname:"europe")


