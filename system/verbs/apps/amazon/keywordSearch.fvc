FrontierVcsFile:3:scpt:system.verbs.apps.amazon.keywordSearch

on keywordSearch (keywords, productline="books", page=1, flDetails=false) {
	«Changes
		«9/9/02; 9:34:50 PM by JES
			«Created. Do a keyword search on Amazon's database, and return the result.
			«Parameters:
				«keywords -- a space-delimited set of keywords for the search
				«productline -- the name of the product line to search
				«page -- the page of results to return
					«results are paged, since there may be *many* results. Default is to return the 1st page
				«flDetails -- if true, the full results including sales ranking and reviews is returned. if false, then only catalog information is returned. default is false.
	local (type = "lite");
	if flDetails {
		type = "heavy"};
	local (t); new (tableType, @t);
	t.keyword = keywords;
	t.page = string (page);
	t.mode = productline;
	t.type = type;
	return (amazon.call (nameOf (this^), t))};
bundle { //test code
	scratchpad.result = keywordSearch ("freedom");
	edit (@scratchpad.result)}


