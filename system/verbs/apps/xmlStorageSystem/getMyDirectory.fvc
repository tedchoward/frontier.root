FrontierVcsFile:3:scpt:system.verbs.apps.xmlStorageSystem.getMyDirectory

on getMyDirectory (email, password, adrtable, adrdata = @xmlStorageSystem.data) {
	«Changes
		«4/7/01; 8:24:04 AM by DW
			«Created. Interfaces to the new xmlStorageSystem entry-point.
		«7/23/01; 9:45:30 PM by JES
			«The data table now supports both xmlRpcPath, and the more general rpcPath, used for both XML-RPC and for SOAP. This is for support for SOAP-based xmlStorageSystem implementations which have a request URI which is different from the value of the SOAPAction header.
	local (params = {"email":email, "password":string.hashmd5 (password)});
	local (rpcPath);
	if defined (adrdata^.xmlRpcPath) {
		rpcPath = adrdata^.xmlRpcPath};
	if defined (adrdata^.rpcPath) {
		rpcPath = adrdata^.rpcPath};
	with adrdata^ {
		local (response = xml.rpc (server, port, "xmlStorageSystem.getMyDirectory", @params, rpcPath:rpcPath, fldebug:fldebug, protocol:protocol, soapAction:soapAction));
		if response.flerror {
			return (false)};
		adrtable^ = response.directory;
		return (true)}};
bundle { //test code
	with user.xmlStorageSystem {
		getMyDirectory (email, password, @scratchpad.myDirectory)};
	edit (@scratchpad.myDirectory)}


