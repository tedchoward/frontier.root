FrontierVcsFile:3:scpt:system.verbs.apps.FinderMenu.commands.findInFile

local (notfound = true);
local (searchcancelled = false);
local (searchstring);
local (openedsomething = false);

on findInFolder (folder) {
	fileloop (f in folder) {
		if file.isFolder (f) {
			if not findInFolder (f) {
				return (false)}}
		else {
			rollBeachBall ();
			msg (file.fileFromPath (f));
			if file.findInFile (f, searchstring) {
				notfound  = false;
				s = "The search string ³" + searchstring + "² was found in ³" + file.fileFromPath (f) + "².";
				s = s + " Would you like to open the file?";
				case dialog.yesNoCancel (s) {
					1 { «Yes
						launch.anything (f);
						openedsomething = true};
					3 { «Cancel
						searchCancellled = true;
						return (false)}}}}};
	return (true)};

if not dialog.ask ("String to search for?", @FinderMenu.commands.searchstring) {
	return};
searchstring = FinderMenu.commands.searchstring;

on visit (path) {
	local (folder = path);
	if not file.isFolder (path) {
		folder = file.folderFromPath (path)};
	return (findInFolder (folder))};
FinderMenu.visitPaths (@visit);

msg (""); «restore Frontier's main window
if (not searchCancelled) and (not openedsomething) {
	if notfound {
		dialog.alert ("³" + searchstring + "² not found.")}
	else {
		dialog.alert ("Search complete.")}}


