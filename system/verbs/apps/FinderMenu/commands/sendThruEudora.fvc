FrontierVcsFile:3:scpt:system.verbs.apps.FinderMenu.commands.sendThruEudora

local (sellist = FinderMenu.getSelectionList ());
if sizeof (sellist) == 0 {
	scriptError ("Select one or more folders before choosing this command.")};
Eudora.launch ();
if not dialog.confirm ("Attach the selected files to a new message in Eudora?") {
	return};
local (f, attachlist = {});
for f in sellist {
	local (attachfile = f);
	if file.isFolder (f) {
		local (folder = file.folderFromPath (Frontier.getProgramPath ()) + "Email Attachments:");
		local (fname = (file.fileFromPath (f) - ":") + ".sit");
		file.sureFolder (folder);
		attachfile = folder + fname;
		stuff.createArchive (attachfile, {f}, false)};
	attachlist = attachlist + attachfile};
if sizeof (attachlist) > 0 {
	local (x = Eudora.createMessage ());
	Eudora.attachFiles (x, attachlist);
	Eudora.bringToFront ()};
return (true)


