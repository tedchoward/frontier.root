FrontierVcsFile:3:scpt:system.verbs.apps.FinderMenu.commands.typeCreatorDialog

local (f, selectionlist = FinderMenu.getSelectionList (), nofiles = true);
if sizeOf (selectionlist) == 0 {
	dialog.alert ("Can't set types and creators because no files are selected in the Finder.");
	return};
for f in selectionlist {
	if not file.isfolder (f) {
		local (origtype = string (file.type (f)));
		local (origcreator = string (file.creator (f)));
		findermenu.cards.data.typestring = origtype;
		findermenu.cards.data.creatorstring = origcreator;
		findermenu.cards.data.prompt = "File ³" + file.fileFromPath (f) + "²:";
		card.run (@findermenu.cards.typeCreator);
		if defined (findermenu.cards.data.ok) {
			delete (@findermenu.cards.data.ok);
			if findermenu.cards.data.typestring != origtype {
				file.settype (f, findermenu.cards.data.typestring)};
			if findermenu.cards.data.creatorstring != origcreator {
				file.setcreator (f, findermenu.cards.data.creatorstring)};
			file.setmodified (file.folderFromPath (f), clock.now ());
			Finder.update (f)};
		nofiles = false}};
if nofiles {
	dialog.alert ("Can't set types and creators because folders don't have type/creator attributes.")}


