FrontierVcsFile:3:scpt:system.verbs.apps.Filemaker.examples.oldExamples.reportStates

«build a list of states and their capitals from the database
	«be sure the database is open before running it

bundle { «create the outline, open it in a window
	new (outlineType, @scratchpad.stateCapitals);
	target.set (@scratchpad.stateCapitals);
	editMenu.setFont ("Geneva");
	editMenu.setFontSize (9);
	edit (@scratchpad.stateCapitals)};

with FileMaker, ObjectModel {
	local (recordCount, i, stateName, stateCapital, dir);
	show (record [all]); «select all records
	recordCount = count (layout [1], record);
	op.setLineText ("There are " + recordCount + " state capitals in the database. They are:");
	dir = right; «first headline inserted to the right
	for i = 1 to recordCount {
		stateName = get (record [i].cell [1]);
		stateCapital = get (record [i].cell [2]);
		op.insert (stateCapital + ", " + stateName, dir);
		dir = down}}; «subsequent headlines inserted down

op.firstSummit ();
window.zoom (@scratchpad.stateCapitals);
target.clear ()


