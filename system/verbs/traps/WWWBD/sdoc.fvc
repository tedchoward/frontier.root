FrontierVcsFile:3:scpt:system.verbs.traps.["WWW½"].sdoc

on sdoc (pathArgs, kfor="", user="", pass="", frmu="", addr="", svpt="",  svnm="", scnm="", meth="", refr="", Kcid="", Kact="", Kapt="", ctyp="", post="", Kcip="", Kfrq="", Agnt="", DIRE="") {
	
	local (paramtable, appID, appName, websiteFolderPath, serverPath);
	new (tableType, @paramtable);
	
	Â«The binaryID should be used in send-partial commands
		Â«in place of the appID or appName. The binaryID specifies
		Â«the application that sent the CGI sdoc event
		Â«specifically. This way you can have two copies of WebSTAR
		Â«(for instance) running at the same time.
		Â«Example:
			Â«webstar.sendpartial (myData, adrparams^.connectionID, true, adrparams^.binaryID)
		Â«[Technically: the binaryID is always either a ProcessSerialNumber or a Target ID Record
			Â«('psn ' or 'targ'), but you don't need to know that to use it.]
	local (binaryID = getEventSender ());
	
	Â«Get info about the server app.
	bundle {
		Â«Get the website folder path.
		if DIRE != "" { Â«DIRE is the website folder parameter
			websiteFolderPath = DIRE}
		else {
			websiteFolderPath = webserver.getPref ("websiteFolderPath")};
		
		Â«Get the server app path and name
		serverPath = webserver.getPref ("serverPath");
		try {
			appName = file.fileFromPath (serverPath)};
		
		Â«Get the server appID
		try {
			appID = file.creator (serverPath)}};
	
	bundle { Â«Set up CGI params table
		paramtable.action = Kact;
		paramtable.actionPath = Kapt;
		paramtable.appID = appID;
		paramtable.appName = appName;
		paramtable.binaryID = binaryID;
		paramtable.clientAddress = addr;
		paramtable.clientIP = Kcip;
		paramtable.connectionID = Kcid;
		paramtable.contentType = ctyp;
		paramtable.fromUser = frmu;
		paramtable.fullRequest = Kfrq;
		paramtable.httpSearchArgs = kfor;
		paramtable.method = meth;
		paramtable.password = pass;
		paramtable.pathArgs = pathArgs;
		paramtable.postargs = post;
		paramtable.referer = refr;
		paramtable.scriptName = scnm;
		paramtable.serverName = svnm;
		paramtable.serverPath = serverPath;
		paramtable.serverPort = svpt;
		paramtable.userAgent = Agnt;
		paramtable.username = user;
		paramtable.websiteFolder = websiteFolderPath;
		Â«Parse the post args, if they exist.
		if paramtable.postargs != "" {
			new (tabletype, @paramtable.argtable);
			webserver.parseArgs (post, @paramtable.argtable)};
		Â«If debugging is on, make a copy of the paramtable.
		if webserver.getPref ("fldebug") {
			scratchpad.cgiparams = paramtable}};
	
	return (webserver.handler (@paramtable))}


