FrontierVcsFile:3:scpt:system.verbs.builtins.mainResponder.members.getMemberTable

on getMemberTable (groupName, memberKey, adrMembers=nil) {
	«Get the address of a member's table, given the name of the group and the member's identifier.
		«Changes:
			«06/01/00; 5:38:39 PM by PBS
				«The parameter to the callback was mailAddress -- which wasn't defined, so it always failed. It should have been memberKey, which it now is. Now callbacks work.
			«9/26/01; 6:15:10 PM by PBS
				«New optional parameter -- adrMembers. Performance improvement. If we can avoid calling mainResponder.members.getMembershipTable, that helps performance.
	
	if adrMembers == nil { //PBS 09/26/01: adrMembers is now an optional parameter
		adrMembers = mainResponder.members.getMembershipTable (groupName)};
	local (adrMember = nil);
	bundle { //set adrMember thru callback, if it's defined
		try {
			local (adrCallback = @adrMembers^.callbacks.getMemberTableAddress);
			if defined (adrCallback^) {
				adrMember = adrCallback^ (memberKey)}}}; //PBS 06/01/00: parameter should be memberKey -- mailAddress wasn't defined
	if adrMember == nil {
		adrMember = @adrMembers^.users.[memberKey]};
	
	return (adrMember)}


