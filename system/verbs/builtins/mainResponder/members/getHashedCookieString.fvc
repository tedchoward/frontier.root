FrontierVcsFile:3:scpt:system.verbs.builtins.mainResponder.members.getHashedCookieString

on getHashedCookieString (adrGroup, memberKey, password, groupName, flExpireAtEndOfSession=false) {
	«Create a new-style cookie string.
		«04/10/00; 12:10:50 PM by PBS
		«Changes:
			«9/12/01; 3:45:16 PM by PBS
				«New parameter: flExpireAtEndOfSession. If true, then any expires setting is over-ridden, and the cookie expires at end of session.
	
	local (s);
	s = string.innerCaseName (adrGroup^.cookieName) + "=";
	
	bundle { //create the value string
		local (t);
		new (tableType, @t);
		t.id = memberKey;
		local (hashString = mainResponder.members.getHash (password, groupName));
		t.h = hashString;
		local (argString = webserver.encodeArgs (@t));
		s = s + argString + "; "};
	
	if adrGroup^.cookieDomain != "" {
		s = s + "domain=" + adrGroup^.cookieDomain + "; "};
	if (not flExpireAtEndOfSession) and (sizeof (adrGroup^.cookieExpires) > 0) {
		s = s + "expires=" + adrGroup^.cookieExpires + "; "};
	s = s + "path=/";
	
	return (s)}


