FrontierVcsFile:3:scpt:system.verbs.builtins.mainResponder.adminSite.website.inetdLog

on inetdLog (maxitems=200) {
	«Changes
		«8/2/02; 2:23:36 PM by JES
			«Created.
	local (pta = html.getPageTableAddress ());
	pta^.title = "inetd Log";
	local (colors = {pta^.trbg1, pta^.trbg2});
	local (columns = {"Time", "Item"});
	on renderItemCallback (adritem) {
		local (itemtext = "");
		on add (s) {
			itemtext = itemtext + (s + "\r");};
		on td (s, align="") {
			if align != "" {
				align = " align=\"" + align + "\""};
			return ("<td" + align + " valign=\"top\"><font size=\"-1\" class=\"small\">" + s + "</font></td>")};
		with adritem^ {
			add (td (string.replaceAll (string.popLeading (string.nthField (xml.convertToDisplayName (nameOf (adrItem^)), ";", 2), ' '), " ", "&nbsp;"), "right" ));
			add (td (html.data.standardMacros.hierarchicTableDisplayer (adritem)))};
		return (itemtext)};
	local (ctitems);
	local (logtext = mainResponder.log.browseAny ("inetd", @renderItemCallback, colors, columns, 3, maxitems, {}, @ctitems));
	if ctitems {
		return (logtext)}
	else {
		return (logtext + "<p>There are no items to list at this time.</p>")}}
	«return (mainResponder.log.browser (50, colors, "small", true, 12, false, maxitems))


