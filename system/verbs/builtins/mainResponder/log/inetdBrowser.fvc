FrontierVcsFile:3:scpt:system.verbs.builtins.mainResponder.log.inetdBrowser

on inetdBrowser (logname="inetd") { //no formatting on the tables
	«Changes:
		«4/14/00; 5:38:19 PM by JES
			«localized
	local (htmltext = "", indentlevel = 0);
	on add (s) {
		htmltext = htmltext + string.filledString ("\t", indentlevel) + s + "\r"};
	local (adrlog);
	bundle { //set adrlog to point to the inetd log for the current hour
		adrlog = log.getGuestSubTable (logname);
		local (ct = sizeof (adrlog^));
		if ct == 0 {
			add (mainResponder.getString ("log.theInetdLogIsEmpty")); // 4/14/99 JES: localized
			return (htmltext)};
		adrlog = @adrlog^ [ct]};
	on td (s) {
		return ("<td valign=\"top\"><font size=\"-0\">" + s + "</font></td>")};
	add ("<table cellpadding=\"5\" border=\"1\">"); indentlevel++;
	local (ct = sizeof (adrlog^), i, adritem);
	for i = ct downto 1 {
		adritem = @adrlog^ [i];
		add ("<tr>"); indentlevel++;
		bundle { //add time
			local (s = xml.convertToDisplayName (nameof (adritem^)));
			s = string.nthfield (s, ';', 2);
			add (td ("<b><pre>" + (s - " " - " ") + "</pre></b>"));
			add (td (html.data.standardMacros.hierarchicTableDisplayer (adritem)))};
		add ("</tr>"); indentlevel--};
	add ("</table>"); indentlevel--;
	return (htmltext)}
«bundle //test code
	«schedulerBrowser ()


