FrontierVcsFile:3:scpt:system.verbs.builtins.log.getGuestSubTable

on getGuestSubtable (logname) {
	«Changes
		«5/12/11; 5:01:50 AM by DW
			«When the date rollover routine fails, which does happen, we're left with an empty guest database. With this change, we fix the problem before continuing. 
		«1/8/99; 9:20:55 AM by DW, work around kernel bug
			«when log.getCurrentFile is called from an agent at midnight it fails to create the table, so we miss logging that one event until the kernel is fixed. in the meantime, I want to catch the next items without failing.
		«12/14/98; 8:13:56 PM by DW
			«This is the basic log access routine. Give me the name of a log, and I'll return you the address of the table containing its log.
	local (f = log.getCurrentFile (), adrsubtable);
	bundle { //set adrsubtable
		try {
			adrsubtable = @[f] [1].[logname]}
		else { //make sure the first top-level item is a table, 5/12/11 by DW
			adrsubtable = @[f].[date.shortstring (clock.now ())];
			if not defined (adrsubtable^) {
				new (tabletype, adrsubtable)};
			adrsubtable = @adrsubtable^.[logname];
			try {delete (@[f].["item #1"])}}};
	if not defined (adrsubtable^) {
		new (tabletype, adrsubtable)};
	return (adrsubtable)}
«bundle //test code
	«getGuestSubtable ("testing")


