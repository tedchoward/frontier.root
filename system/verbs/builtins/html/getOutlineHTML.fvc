FrontierVcsFile:3:scpt:system.verbs.builtins.html.getOutlineHTML

on getOutlineHTML (adroutline, indentstring, outdentstring, linestartstring, lineendstring, flprettyPrint=true) {
	try { //only works on Mac OS
		return (html.ucmds.getOutlineHTML (adroutline, indentstring, outdentstring, linestartstring, lineendstring))}
	else {
		local (level = 0);
		local (htmltext = "");
		on add (s) {
			htmltext = htmltext + s};
		on addLine (s) {
			if flPrettyPrint {
				add (string.filledString ("\t", level) + s + "\r")}
			else {
				add (s)}};
		on visit () {
			loop {
				addLine (linestartstring + op.getLineText () + lineendstring);
				if op.go (right, 1) > 0 {
					level++;
					addLine (indentstring);
					visit ();
					addLine (outdentstring);
					level--;
					op.go (left, 1)};
				if not op.go (down, 1) {
					break}}};
		
		local (oldtarget = target.get ());
		target.set (adroutline);
		op.firstSummit ();
		op.fullExpand ();
		if flPrettyPrint {
			add ("\r")};
		visit ();
		try {target.set (oldtarget)};
		return (htmltext)}}


