FrontierVcsFile:3:scpt:system.verbs.builtins.html.utilities.findAllHREFs

on findAllHREFs (adrtable, adroutline) {
	on visit (adrobject) {
		local (type = typeof (adrobject^));
		if (type != tableType) and (type != stringtype) {
			local (s = string (adrobject^));
			if string.lower (s) contains "<a href" {
				op.insert (adrobject, down);
				local (dir = right);
				loop {
					local (ct, ix = string.patternmatch ("<a href", string.lower (s)));
					if ix == 0 {
						if dir == down {
							op.go (left, 1)};
						break};
					s = string.delete (s, 1, ix - 1);
					ct = string.patternmatch ("</a>", string.lower (s)) + 3;
					href = string.mid (s, 1, ct);
					s = string.delete (s, 1, ct);
					op.insert (href, dir);
					dir = down}}}};
	new (outlinetype, adroutline);
	edit (adroutline);
	target.set (adroutline);
	table.visit (adrtable, @visit);
	editmenu.setfont ("geneva");
	editmenu.setfontsize (9);
	op.firstsummit ();
	op.deleteline ();
	window.zoom (adroutline)};
findAllHREFs (@user.websites.frontier, @scratchpad.urllist)


