FrontierVcsFile:3:scpt:system.verbs.builtins.html.directory.getCanonicalName

on getCanonicalName (linetext, flStripMarkup=false) {
	«Changes
		«9/23/12; 11:34:19 AM by DW
			«New optional param, flStripMarkup. If true we first remove all markup before generating the canonical name.
				«http://worknotes.scripting.com/september2012/92312ByDw/permalinksOnCommentPages
		«7/3/11; 9:42:49 PM by DW
			«If the canonical name ends up being the empty string, then make the name the literal linetext. This came up in a headline whose text is :-). No alpha characters in there, but it's still possible to make a URL out of it.
		«5/13/11; 7:12:37 PM by DW
			«Respect new pref that says if the hyphen is or is not a special character.
		«5/12/02; 12:14:05 AM by JES
			«Drop all characters whose ascii value is above 127.
		«11/6/2000; 6:12:56 PM by DW
			«Take everything to the left of the hyphen (if present), remove all punctuation, and innerCase it. This is the string we use to refer to the directory page in a URI.
	local (adrdata = html.directory.init (), i, name = linetext, ch);
	if flStripMarkup { //9/23/12 by DW
		name = searchengine.stripmarkup (name)};
	if adrdata^.prefs.flHyphenIsSpecial {
		for i = 1 to sizeof (name) {
			if name [i] == '-' {
				if i < sizeof (name) {
					if name [i + 1] == ' ' {
						name = string.trimwhitespace (string.mid (name, 1, i));
						break}}}}};
	for i = sizeof (name) downto 1 {
		ch = name [i];
		if (not string.isalpha (ch)) and (not string.isnumeric (ch)) and (ch != ' ') {
			name = string.delete (name, i, 1)}
		else {
			if number (ch) > 127 {
				name = string.delete (name, i, 1)}}};
	name = string.innerCaseName (name);
	if sizeof (name) == 0 {
		name = linetext};
	return (name)}
«bundle //test code
	«dialog.alert (getCanonicalName ("<a href=\"http://static.scripting.com/pensacola/comments/2012/09/23/00116.opml\">OPML source</a>", true))
	«dialog.alert (getCanonicalName (":-)"))
	«dialog.alert (getCanonicalName ("xml-rpc"))


