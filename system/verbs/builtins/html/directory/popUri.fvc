FrontierVcsFile:3:scpt:system.verbs.builtins.html.directory.popUri

on popuri (uri, levels) {
	«Changes
		«1/25/12; 11:38:52 AM by DW
			«If the uri is empty, return the empty string.
	on skiptosquiggle (i) {
		local (j);
		for j = i - 1 downto 1 {
			«msg (string.mid (uri, 1, j))
			if uri [j] == "~" {
				return (j - 1)}}};
	if sizeof (uri) == 0 { //1/25/12 by DW
		return ("")};
	local (i, ct = 0, flskip, sizeuri = sizeof (uri));
	if levels == 0 {
		return (uri)};
	i = sizeuri;
	if uri [i] == "~" {
		i = skiptosquiggle (i)};
	loop {
		if i < 1 {
			return ("")};
		«msg (string.mid (uri, 1, i))
		if uri [i] == "/" {
			if ++ct == levels {
				return (string.mid (uri, 1, i - 1))};
			i--;
			if i < 1 {
				return ("")};
			«msg (string.mid (uri, 1, i))
			if uri [i] == "~" {
				i = skiptosquiggle (i)}}
		else {
			i--}}};
bundle { //test code
	«dialog.alert (popuri ("theWeb/development/contentManagement/~4/userland/software/frontier~", 2))
	dialog.alert (popuri ("", 2));
	dialog.alert (popuri ("theWeb/development/contentManagement", 2))}


