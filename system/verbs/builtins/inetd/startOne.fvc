FrontierVcsFile:3:scpt:system.verbs.builtins.inetd.startOne

on startOne (daemonTableAdr) {
	«Change Notes
		«1/16/00; 11:58:55 AM by RAB
			«In Frontier 6.2a4 the daemon table can contain an item called "ip" which tells Frontier which dotted IP address it should listen on.
		«Mon, Apr 24, 2000 at 8:19:54 PM by AR
			«The IP address to listen on can now also be specified in the PPC version of Frontier.
			«Starting with 6.2b8, the additional listeners are no longer needed on PPC Macs.
	local (listenRef);
	user.inetd.shutdown = false;
	if inetd.isDaemonRunning (daemonTableAdr) { //a daemon is already running on the port
		return (false)};
	with daemonTableAdr^ {
		local (addr=0);
		if defined (ip) { //6.2a4
			addr = ip};
		listenRef = tcp.listenStream (port, count, @inetd.supervisor, port, addr);
		new (tableType, @user.inetd.listens.[port]);
		user.inetd.listens.[port].ref = listenRef;
		user.inetd.listens.[port].adrTable = daemonTableAdr;
		if system.environment.isMac { //04/24/00 AR
			if date.versionLessThan (Frontier.version (), "6.2b8") or (not Frontier.isPowerPC ()) {
				local (i);
				for i = 2 to count / 2 {
					listenRef = tcp.listenStream (port, 1, @inetd.supervisor, port, addr);
					user.inetd.listens.[port].["ref" + i] = listenRef}}};
		return (true)}};

startOne (@user.inetd.config.http)


