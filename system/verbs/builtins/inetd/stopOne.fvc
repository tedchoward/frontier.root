FrontierVcsFile:3:scpt:system.verbs.builtins.inetd.stopOne

on stopOne (adrDaemonTable) {
	«Change Notes
		«Script: system.verbs.builtins.inetd.stopOne; Version 1; Date: Thu, 14 May 1998 03:35:26 GMT; ID: RAB
		«Mon, Apr 24, 2000 at 8:21:26 PM by AR
			«Starting with 6.2b8, the additional listeners are no longer needed on PPC Macs.
	local (nameDaemon = nameOf (adrDaemonTable^));
	local (port = adrDaemonTable^.port);
	if not defined (user.inetd.listens.[port].ref) {
		scriptError ("Can't stop \"" + nameDaemon + "\" daemon because it hasn't been started.")};
	try {
		tcp.closeListen (user.inetd.listens.[port].ref);
		if system.environment.isMac { //04/24/00 AR
			if date.versionLessThan (Frontier.version (), "6.2b8") or (not Frontier.isPowerPC ()) {
				local (i);
				for i = 2 to adrDaemonTable^.count / 2 {
					tcp.closeListen (user.inetd.listens.[port].["ref" + i])}}};
		delete (@user.inetd.listens.[port])}
	else {
		scriptError ("Can't stop \"" + nameDaemon + "\" daemon because \"" + tryerror + ".\"")}}


