FrontierVcsFile:3:scpt:system.verbs.builtins.file.setModified

on setModified (f, when) {
	«Changes
		«11/27/01; 9:06:02 AM by DW
			«Supports callback, patterned after file.writewholefile's new callback.
	on kernelcall (f, when) {
		kernel (file.setmodified)};
	local (val = kernelcall (f, when));
	bundle { //call the callbacks, if present
		local (adrscript);
		if not defined (user.callbacks.fileSetModified) {
			new (tableType, @user.callbacks.fileSetModified)};
		for adrscript in @user.callbacks.fileSetModified {
			try {
				while typeOf (adrscript^) == addressType {
					adrscript = adrscript^};
				adrscript^ (f)}}};
	return (val)}
«bundle //test code
	«setModified ("C:\\Program Files\\Radio UserLand\\www\\index.txt", clock.now ())


