FrontierVcsFile:3:scpt:system.verbs.builtins.webserver.parseArgs

on parseArgs (argstring, adrtable) {
	«Changes
		«9/16/04; 1:49:40 AM by JES
			«If running on MacOS, convert argument names to mac characters.
		«8/18/98; 9:41:51 AM by DW
			«Site of a major security hole in Frontier. Closed.
				«It goes all the way back to June 1995
	local (argslist = string.parseHttpArgs (argstring));
	local (i, name, value, adritem);
	loop (i = 1; i <= sizeof (argslist); i = i + 2) {
		name = argslist [i];
		if system.environment.isMac {
			name =  latinToMac.convert (name)};
		value = argslist [i + 1];
		adritem = @adrtable^.[name];
		if defined (adritem^) { //put multiple selections into list
			if typeOf (adritem^) != listType {
				adritem^ = {adritem^}};
			adritem^ = adritem^ + {value}}
		else {
			adritem^ = value}}}


