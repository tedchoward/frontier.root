FrontierVcsFile:3:scpt:system.verbs.builtins.webserver.data.responders.websiteFramework.condition

on condition (pta) {
	«Changes
		«8/1/02; 3:00:49 AM by JES
			«Condition returns false if the request is on the port specified by user.inetd.config.http2, and we're running in Frontier.
		«1/19/01; 12:17:09 PM by PBS
			«New condition script for the website framework responder.
				«.wsf requests are always handled by the website framework responder.
				«But also -- if the first part of a path matches a name in the docTree, then that request is handled by this responder. This allows for paths like /xxx/. Previously you always had to do /xxx/index.wsf.
	
	if not system.environment.isRadio {
		if defined (user.inetd.config.http2.port) {
			if pta^.port == user.inetd.config.http2.port {
				return (false)}}};
	
	if pta^.path endsWith ".wsf" {
		return (true)};
	
	local (normalPath = string.popLeading (pta^.path, '/'));
	local (firstPart = string.nthField (normalPath, '/', 1));
	local (adrResponder = parentOf (this^));
	local (adrDocTree = @adrResponder^.data.docTree);
	if defined (adrDocTree^.[firstPart]) { //does the first part of the path exist in the docTree?
		return (true)};
	
	return (false)}


