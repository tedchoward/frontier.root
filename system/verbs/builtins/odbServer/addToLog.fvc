FrontierVcsFile:3:scpt:system.verbs.builtins.odbServer.addToLog

on addToLog (label, commandname, atts) {
	if window.isOpen (@user.odbServer.log) {
		local (mainline = string.upper (commandname) + " " + label + " " + clock.now ());
		odbServer.targetSet (@user.odbServer.log);
		op.setDisplay (false);
		op.firstSummit ();
		if op.getLineText () == "" {
			op.setLineText (mainline)}
		else {
			op.insert (mainline, up)};
		
		local (i, dir = right, s, att);
		for i = 1 to sizeof (atts^) {
			att = @atts^ [i];
			s = "#" + nameOf (att^) + " " + displayString (att^) + "";
			op.insert (s, dir); dir = down};
		if dir == down {
			op.go (left, 1)};
		op.setDisplay (true);
		odbServer.targetClear (@user.odbServer.log)}}
«bundle «test code
	«local (atts)
	«new (tabletype, @atts)
	«atts.xxx = clock.now ()
	«atts.yyy = clock.now ()
	«atts.zzz = clock.now ()
	«for i = 1 to 10
		«addToLog ("test", @atts)


