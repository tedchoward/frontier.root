FrontierVcsFile:3:scpt:system.verbs.builtins.launch.anything

on anything (path) {
	«Changes:
		«05/11/01; 6:55:14 PM by JES
			«Implemented for Windows in script, using file.findApplication to find the app, and launch.appWithDocument to open the file. Executible files (.exe, .com, and .bat) are launched using launch.application.
	on kernelCall (path) {
		kernel (launch.anything)};
	case true {
		system.environment.isMac {
			kernelCall (path)};
		system.environment.isWindows {
			if file.isFolder (path) {
				file.openFolder (path)}
			else {
				local (extension = string.nthField (path, ".", string.countFields (path, ".")));
				case string.lower (extension) {
					"exe";
					"com";
					"bat" {
						return (launch.application (path))}}
				else {
					return (launch.appWithDocument (file.findApplication (extension), path))}}}}}
«bundle //test code
	«launch.anything ("C:\\test.txt")


