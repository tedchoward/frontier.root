FrontierVcsFile:3:scpt:system.verbs.builtins.Frontier.tools.checkTools

on checkTools () {
	«This is a lightweight Tools checker.
		«If a Tools changes status, install or uninstall. Also install new Tools.
		«01/06/01; 3:34:11 PM by PBS
	«Changes
		«9/9/01; 2:15:17 PM by JES
			«Call Frontier.tools.cleanToolName to get the short name of the tool.
				«Fixes a bug reported by Philippe Martin 9/9/01 on the frontier-bugs mailing list.
	
	Frontier.tools.installNewTools (); //install any new Tools
	
	on checkOneTool (f) {
		local (fname = file.fileFromPath (f));
		local (shortName = Frontier.tools.cleanToolName (fname));
		
		if Frontier.tools.isEnabled (f) { //possibly install enabled Tool
			local (flInstalled = false);
			if defined (user.tools.databases.[shortName].flInstalled) {
				if user.tools.databases.[shortName].flInstalled {
					flInstalled = true}};
			if not flInstalled {
				Frontier.tools.install (f)}}
		else { //possibly uninstall disabled Tool
			if defined (user.tools.databases.[shortName].flInstalled) {
				if user.tools.databases.[shortName].flInstalled {
					Frontier.tools.unInstall (f)}}};
		
		return (true)}; //keep visiting
	
	Frontier.tools.visitOpenTools (@checkOneTool)};
bundle { //testing
	checkTools ()}


