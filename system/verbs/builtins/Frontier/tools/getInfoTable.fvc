FrontierVcsFile:3:scpt:system.verbs.builtins.Frontier.tools.getInfoTable

on getInfoTable () {
	«Return a table containing info about the Tools in the Tools folder. Each Tool gets a sub-table whose name is the filename of the Tool. Each sub-table contains:
		«f -- the path on disk
		«flEnabled -- whether it's enabled or not
		«flInstalled -- whether it's installed or not
		«flOpen -- whether the gdb is open or not
		«01/08/01; 1:15:30 PM by PBS
	
	local (f, folder = Frontier.tools.getToolsFolderPath ());
	local (infoTable);
	new (tableType, @infoTable);
	fileloop (f in folder) {
		local (fname = file.fileFromPath (f));
		local (shortName = string.popSuffix (fname));
		local (adrItem = @infoTable.[fname]);
		new (tableType, adrItem);
		adrItem^.f = f;
		adrItem^.flOpen = defined ([f]);
		adrItem^.flEnabled = false; //default
		adrItem^.flInstalled = false; //default
		
		bundle { //is the Tool installed?
			if defined (user.tools.databases.[shortName].flInstalled) {
				adrItem^.flInstalled = user.tools.databases.[shortName].flInstalled}};
		
		bundle { //is the Tool enabled?
			if adrItem^.flOpen {
				if Frontier.tools.isEnabled (f) {
					adrItem^.flEnabled = true}}}};
	
	return (infoTable)}


