FrontierVcsFile:3:scpt:system.verbs.builtins.Frontier.hideApplication

on hideApplication () {
	if system.environment.isMac { //send Apple event
		if system.environment.isCarbon { //call System Events.app
			local (systemEventsId = 'sevs');
			if not (sys.appIsRunning (systemEventsId)) { //Is System Events.app running?
				local (f = file.getSystemDisk () + "System:Library:CoreServices:System Events.app:");
				launch.application (f)};
			local (processName = file.fileFromPath (Frontier.getProgramPath ()));
			local (obj = objspec (['prcs'] [processName].['pvis']));
			return (appleEvent ('sevs', 'core', 'setd', '----', obj, 'data', false))}
		else { //Classic -- call Finder
			local (processName = file.fileFromPath (Frontier.getProgramPath ()));
			with objectModel, Finder {
				return (set (process [processName].visible, false))}}};
	if system.environment.isWindows { //kernel verb
		on kernelCall () {
			kernel (Frontier.hideApplication)};
		return (kernelCall ())}}


