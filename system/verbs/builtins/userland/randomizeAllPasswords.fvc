FrontierVcsFile:3:scpt:system.verbs.builtins.userland.randomizeAllPasswords

on randomizeAllPasswords (adrInRoot) {
	«Changes
		«5/1/10; 11:53:51 AM by DW
			«Given an address in an object database, reset all objects named password or secretSubject to a random string of 10 characters. This makes it possible to ship userland.com websites with the user information intact, without revealing any passwords. The email addresses were already public, so nothing is revealed there that wasn't already available. 
	local (alist = string.parseaddress (adrInRoot), ct = 0, flmessages = true, ctrandomized = 0);
	on randomizePasswords (adrtable) {
		local (adr);
		for adr in adrtable {
			if (++ct % 100) == 0 {
				msg (ct + ": " + string.popFileFromAddress (adr))};
			if typeof (adr^) == tabletype {
				randomizePasswords (adr)}
			else {
				local (lowername = string.lower (nameof (adr^)));
				if (lowername == "password") or (lowername == "secretsubject") {
					adr^ = string.getrandompassword (10);
					ctrandomized++}}}};
	randomizePasswords (@[alist [1]]);
	return (ctrandomized)};
bundle { //test code
	local (adrroot = @["Macintosh HD:Users:davewiner:Desktop:OPML:Guest Databases:www:docserver.root"]);
	dialog.alert (randomizeAllPasswords (adrroot) + " passwords randomized.")}


