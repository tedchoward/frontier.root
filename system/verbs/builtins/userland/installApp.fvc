FrontierVcsFile:3:scpt:system.verbs.builtins.userland.installApp

on installApp (appRoot) {
	«Install one app in the Guest Databases/apps/ folder.
		«appRoot is the name of the gdb, as in mainResponder.root or manila.root.
		«This script:
		«1) Open the gdb if it's not open.
		«2) Creates an entry in user.databases.
		«3) Runs the startup script.
			«10/26/99; 6:18:10 PM by PBS
	
	local (f = Frontier.getSubFolder ("apps") + appRoot);
	if not defined ([f]) {
		fileMenu.open (f, true)}; //open it hidden
	
	«Add an entry to the user.databases table.
	local (adrTable = @user.databases.[appRoot]);
	new (tableType, adrTable);
	adrTable^.f = f;
	adrTable^.openOnStartup = true;
	adrTable^.runStartupScript = true;
	adrTable^.supportsSubscribe = false;
	
	«Run the startup script.
	adrStartupScript = @[f].["#startup"];
	if defined (adrStartupScript^) {
		adrStartupScript^ ()}; //run the startup script
	
	return (true)}


