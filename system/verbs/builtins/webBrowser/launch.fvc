FrontierVcsFile:3:scpt:system.verbs.builtins.webBrowser.launch

on launch () {
	«Changes:
		«10/31/07; 9:35:51 AM by DW
			«Call webbrowser.launchMacBrowser first. If it can't launch a browser, fall through to the broken code (it really is a mess).
		«1/9/02; 12:05:13 PM by PBS
			«Must use actual codes because on OS X the app IDs may be '    ' since apps are bundles (folders) sometimes.
	local (id);
	if webBrowser.isRunning () {
		return (user.webBrowser.currentid )}; // a supported browser is already running.
	id = webBrowser.getDefaultBrowser ();
	if (id == false) or (id == 'NONE') {
		return (false)};
	case sys.os () {
		"MacOS" {
			if webbrowser.launchMacBrowser () { //10/31/07; 9:35:23 AM by DW
				return};
			if sys.appIsRunning (id) {
				return};
			case id {
				'MOSS';
				'MOZZ';
				Netscape.id {
					if app.start (@Netscape) {
						user.webBrowser.currentid = Netscape.id;
						webBrowser.bringToFront ();
						return (id)}};
				'MSIE';
				msExplorer.id {
					if app.start (@msExplorer) {
						user.webBrowser.currentid = msExplorer.id;
						webBrowser.bringToFront ();
						return (id)}}};
			scriptError ("Can't launch the web browser. Please check your preference with Internet Config.")};
		"Win95";
		"WinNT" {
			if sys.appIsRunning (file.fileFromPath (id)) {
				return (file.fileFromPath (id))};
			if launch.appWithDocument (id, "") {
				user.webBrowser.currentid = file.fileFromPath (id);
				clock.waitSeconds (2);
				return (file.fileFromPath (id))}}}}


