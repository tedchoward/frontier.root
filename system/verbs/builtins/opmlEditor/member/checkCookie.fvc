FrontierVcsFile:3:scpt:system.verbs.builtins.opmlEditor.member.checkCookie

on checkCookie (urlRedirect, adrusername, flredirect=true, adrpassword=nil, groupname="default") {
	«Changes
		«12/16/12; 7:07:38 PM by DW
			«New optional param, groupname. Pass groupname to opmlEditor.member.validate.
				«http://worknotes.scripting.com/december2012/121612ByDw/usingGroupsInOpmlServerApps
		«4/11/12; 3:51:14 PM by DW
			«New optional param, adrpassword. If non-nil, we return the password to the caller. 
		«8/28/11; 11:59:28 AM by DW
			«Set pta^.flLocalAdminUser if it's the local user acting as "admin." This is used in building a menu.
		«4/3/11; 4:25:32 PM by DW
			«Call opmleditor.init. If the request is coming from the local machine, and we can't validate the user via cookies, the user is automatically "admin" -- if the pref is set true.
		«3/7/11; 7:11:23 PM by DW
			«Don't look for the specific cookie if there are no cookies at all.
		«3/7/11; 8:36:59 AM by DW
			«Return true if the user has the cookie and a valid username and password, else redirect to the indicated address.
				«The cookie looks like this: "username%3Ddave%26password%3Dbloing"
			«Added optional flredirect param. Our menu code wants to display an abbreviated menu if the user is not a member, so it doesn't want the redirect.
	local (pta = html.getpagetableaddress (), now = clock.now ());
	local (adrgroup = opmlEditor.member.initGroup (groupname));
	opmlEditor.init ();
	if defined (pta^.requestHeaders.cookies) {
		local (adrcookie = @pta^.requestHeaders.cookies.[adrgroup^.cookiename]);
		if defined (adrcookie^) {
			local (args);
			new (tabletype, @args);
			webserver.parseargs (string.urldecode (adrcookie^), @args);
			if defined (args.username) and defined (args.password) {
				if opmlEditor.member.validate (args.username, args.password, groupname) {
					adrusername^ = args.username;
					if adrpassword != nil { //4/11/12 by DW
						adrpassword^ = args.password};
					return (true)}}}};
	bundle { //allow access for local user, as admin, 4/3/11 by DW
		if user.opmlEditor.prefs.flLocalMemberIsAdmin {
			if opmlEditor.isSameMachine (pta^.client) {
				local (username = "admin");
				local (adrmember = opmlEditor.member.initMember (username, flcreate:true));
				adrmember^.stats.ctLocalAccessess++;
				adrmember^.stats.whenLastLocalAccess = now;
				adrusername^ = username;
				pta^.flLocalAdminUser = true; //8/28/11 by DW
				return (true)}}};
	if flredirect {
		webserver.redirect (pta, urlRedirect)};
	return (false)};
bundle { //test code
	html.setpagetableaddress (@scratchpad.radio2params);
	checkcookie ("http://scripting.com/")}


