FrontierVcsFile:3:scpt:system.verbs.builtins.menu.addSuite

on addSuite (suiteadr) {
	if not defined (suiteadr^) {
		local (name);
		bundle { «klooge to get the name part of the address
			«new (tableType, suiteadr)
			suiteadr^ = 0;
			name = nameOf (suiteadr^);
			delete (suiteadr)};
		local (f = Frontier.pathstring + "Suites:" + name);
		if file.exists (f) {
			local (s);
			speaker.beep ();
			s = "The \"" + name + "\" suite isn't in Frontier.root.";
			s = s + " Import it from your Suites folder?";
			if not dialog.confirm (s) {
				return (false)};
			if not Frontier.finder2Click (f) {
				return (false)};
			suiteadr = address (suiteadr)}
		else {
			local (s);
			s = "There is no suite named \"" + name + "\" in Frontier.root, or in your Suites folder.";
			scriptError (s)}};
	
	if not menu.clearMenubar () {
		return (false)};
	
	if not menu.installMainMenu () {
		return (false)};
	
	if not suiteadr^.installmenu () {
		return (false)};
	
	system.menus.data.currentSuite = suiteadr;
	return (true)}


