FrontierVcsFile:3:scpt:system.verbs.builtins.batchExporter.init

«Changes:
	«1/3/02; 4:32:48 PM by DW
		«Remove references to user.playlist, add user.radio.
	«12/03/00; 2:58:42 PM by PBS
		«Radio UserLand changes -- set folder to Backups folder; list of items to export is different.
	«12/05/00; 12:33:48 PM by PBS
		«Save user.inetd.config.http -- server prefs.
	«12/16/00; 6:58:42 PM by JES
		«Save user.webEdit: webEdit prefs.
	«Sat, Nov 1, 1997 at 7:19:33 AM by DW
		«Made it cross-platform, also created the log outline if it doesn't exist.
if not defined (user.batchExporter) {
	new (tabletype, @user.batchExporter)};
if not defined (user.batchExporter.folder) {
	local (folder = file.folderFromPath (Frontier.getProgramPath ()));
	local (pc = file.getPathChar ());
	if system.environment.isPike { //Radio UserLand
		user.batchExporter.folder = folder + "Backups" + pc}
	else { //Frontier
		user.batchExporter.folder = folder + "Batch Exporter" + pc}};
if not defined (user.batchExporter.log) {
	new (outlineType, @user.batchExporter.log)};
if not defined (user.batchExporter.keepLog) {
	if system.environment.isPike {
		user.batchExporter.keepLog = false}
	else { //Frontier
		user.batchExporter.keepLog = true}};
if not defined (user.batchExporter.list) {
	if system.environment.isRadio {
		new (outlineType, @user.batchExporter.list);
		target.set (@user.batchExporter.list);
		op.insert ("workspace", down);
		op.insert ("websites", down);
		op.insert ("user.menus", down);
		op.insert ("user.radio", down);
		op.insert ("user.prefs", down);
		op.insert ("user.inetd.config.http", down); //PBS 12/05/00: save http server prefs
		op.insert ("user.webedit", down); //JES 12/16/00: save webEdit prefs
		op.firstSummit ();
		op.deleteLine ();
		target.clear ()}
	else { //Frontier
		new (outlineType, @user.batchExporter.list);
		target.set (@user.batchExporter.list);
		op.insert ("websites", down);
		op.insert ("workspace", down);
		op.insert ("user", down);
		op.insert ("suites", down);
		op.insert ("system.agents", down);
		op.insert ("system.extensions", down);
		if sys.os () == "MacOS" {
			op.insert ("system.verbs.traps", down)};
		op.firstSummit ();
		op.deleteLine ();
		target.clear ()}}


