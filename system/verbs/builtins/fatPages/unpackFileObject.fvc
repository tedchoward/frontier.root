FrontierVcsFile:3:scpt:system.verbs.builtins.fatPages.unpackFileObject

on unpackFileObject (adrAtts, path, flRunnable=false) {
	local (objectType, data);
	objectType = fatPages.getObjectType (adrAtts);
	if not objectType {
		fatPages.noObjectError (path)};
	data = fatPages.decode (@adrAtts^.pageData);
	delete (@adrAtts^.pageData); //we're done with it, free memory now
	setBinaryType (@data, objectType);
	local (alreadyOpen = defined ([path]));
	if alreadyOpen {
		if flRunnable {
			scriptError ("\"" + file.fileFromPath (path) + "\" is already running.")}
		else {
			window.bringToFront (path)};
		return (true)};
	unpackWindow (@data, path);
	if flRunnable { //it's a desktop script
		«File-based, runnable desktop script
		system.deskscripts.path = path;
		[path] ();
		window.close (path)}
	else { //it's not a desktop script
		«File-based window
		window.show (path);
		window.bringToFront (path)};
	return (true)}


