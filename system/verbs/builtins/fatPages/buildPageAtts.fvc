FrontierVcsFile:3:scpt:system.verbs.builtins.fatPages.buildPageAtts

on buildPageAtts (adrPageTable) {
	local (htmltext = "");
	on add (s) {
		htmltext = htmltext + s};
	add ("\r<!--\r#fatPage\r"); «Wed, Feb 26, 1997 at 7:44:57 AM by DW, added extra \r before #
	on addHint (name, value, flencode=false) {
		local (s = string (value));
		if flencode {
			s = base64.encode (string (s), 0)};
		add ("#" + name + " " + s + "\r")};
	addHint ("version", 1);
	with adrPageTable^ {
		addHint ("docs", "http://www.scripting.com/fatPages/faq.html");
		addHint ("adrObject", adrobject);
		addHint ("netAddress", html.getPref ("serverNetAddress"));
		addHint ("objectCreated", date.netStandardString (timeCreated (adrobject)));
		addHint ("objectModified", date.netStandardString (timeModified (adrobject)));
		addHint ("lastBuildTime", date.netStandardString (clock.now ()));
		if defined (menubar) { «Tue, Nov 26, 1996 at 5:13:22 AM by DW
			local (data);
			pack (address (menubar)^, @data);
			data = string.urlEncode (data);
			addHint ("menubar", data)};
		if defined (adrPageData) { «Thu, Mar 20, 1997 at 7:24:45 AM by DW
			addHint ("adrPageData", adrPageData);
			addHint ("objectType", "application/x-frontier-" + typeOf (adrPageData^));
			addHint ("pageData", fatPages.encodePageData (adrPageData), true)}};
	add ("-->\r");
	return (htmltext)}


