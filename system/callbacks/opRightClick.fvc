FrontierVcsFile:3:scpt:system.callbacks.opRightClick

on opRightClick () {
	«Created, Friday, August 18, 2000 at 2:28:24 PM by DW
		«This script is called when the user right-clicks in any outline. In turn it calls all the scripts in user.callbacks.opRightClick. 
		«The first callback that returns a menu consumes the event, and we return the address they return to us.
	«Changes:
		«Thursday, September 28, 2000 at 6:15:29 PM by JES
			«Allow the callback to be an address or the address of an address...
	local (adrscript, adrmenu = nil);
	if not defined (user.callbacks.opRightClick) {
		new (tableType, @user.callbacks.opRightClick)};
	for adrscript in @user.callbacks.opRightClick {
		try {
			while typeOf (adrScript^) == addressType {
				adrScript = adrScript^};
			adrscript^ (@adrmenu);
			if adrmenu != nil {
				return (adrmenu)}}}}


