FrontierVcsFile:3:scpt:suites.commercial.createDocOutline

«Sat, Dec 6, 1997 at 1:41:11 AM by PBS
	«Changed references to commercial.appname to temp.appname.
local (adrTable, adrOutline, i);
adrTable = @[temp.appname];
adrOutline = @scratchpad.[temp.appname + "Docs"];
if defined (adrOutline^) {
	delete (adrOutline)};
new (outlineType, adrOutline);
target.set (adrOutline);
editMenu.setFont ("Palatino");
editMenu.setFontSize (12);
target.set (adrOutline);
for i = 1 to sizeof (adrTable^) {
	local (adr = @adrTable^ [i]);
	rollbeachball ();
	if typeof (adr^) != scriptType {
		continue};
	local (syntax = string.nthField (string (adr^), cr, 1));
	syntax = string.nthField (syntax, "{", 1); «remove comment, if present
	syntax = temp.appname + '.' + (syntax - "on ");
	rollbeachball ();
	op.insert (temp.appname + '.' + nameOf (adr^), down);
	op.insert ("Syntax", right);
	op.insert (syntax, right);
	op.insert ("Parameters", left);
	op.insert ("Action" + cr + "Returns" + cr + "Examples" + cr + "Notes" + cr + "See Also", down);
	op.go (left, 1)};
window.open (adrOutline)


