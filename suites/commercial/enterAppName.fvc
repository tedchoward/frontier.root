FrontierVcsFile:3:scpt:suites.commercial.enterAppName

if dialog.ask ("Enter the name for your app's ³Glue² table:", @temp.appname) {
	local (somethingCreated = false);
	local (adrGlueTable = @system.verbs.apps.[temp.appname]);
	if not defined (adrGlueTable^) {
		on replaceAll (searchstring, replacestring) {
			local (numreplaced, savedparams);
			
			table.assign (@savedparams, user.prefs.search); «save off
			
			user.prefs.search.flatsearch = false;
			user.prefs.search.frontwindowonly = false;
			user.prefs.search.wholewords = false;
			
			user.prefs.search.searchfor = searchstring;
			user.prefs.search.replacewith = replacestring;
			
			search.reset ();
			numreplaced = search.replaceall ();
			
			table.assign (@user.prefs.search, savedparams); «restore originals
			
			return (numreplaced)};
		if not dialog.confirm ("Create a new glue script table for " + temp.appname + "?") {
			return};
		new (tabletype, adrGlueTable);
		table.copycontents (@commercial.newTable, adrGlueTable);
		target.set (adrGlueTable);
		replaceAll ("Sample", temp.appname);
		target.clear ();
		somethingCreated = true;
		sys.systemTask ()};
	
	local (adrAppInfo = @adrGlueTable^.appInfo);
	if not defined (adrAppInfo^) {
		if not dialog.confirm ("Create a new appInfo table for " + temp.appname + "?") {
			return};
		new (tabletype, adrAppInfo);
		somethingCreated = true;
		adrAppInfo^.app1supported = false;
		adrAppInfo^.name = temp.appname;
		adrAppInfo^.sharedMenus = dialog.yesNo ("Does " + temp.appname + " support Menu Sharing?");
		adrAppInfo^.path = "";
		adrAppInfo^.id = '????'};
		«if not dialog.ask ("What is " + temp.appname + "'s creator id?", @adrAppInfo^.id)
			«return
		«adrAppInfo^.id = string4 (adrAppInfo^.id)
		«adrGlueTable^.id = adrAppInfo^.id
	
	local (promptForFile = false);
	bundle { «determine if we should prompt for the file
		local (f = adrAppInfo^.path);
		if f == "" {
			promptForFile = true}
		else {
			if file.exists (f) {
				if file.type (f) != 'APPL' {
					promptForFile = true}}
			else {
				promptForFile = true}}};
	if promptForFile {
		adrAppInfo^.path = file.findApplication (adrAppInfo^.id);
		if not file.getFileDialog ("Where is " + temp.appname + "?", @adrAppInfo^.path, 'APPL') {
			return};
		adrAppInfo^.id = file.creator (adrAppInfo^.path);
		adrGlueTable^.id = adrAppInfo^.id};
	
	if somethingCreated { «seed table with scripts if table or appInfo is new
		if rez.countResources (adrAppInfo^.path, 'aete') > 0 {
			if dialog.confirm ("Create glue scripts based on " + temp.appname + "¹s 'aete' resource?") {
				commercial.importAeteInfo ()}}};
	
	if adrAppInfo^.sharedMenus {
		local (adrMenuBar = @system.menus.sharedMenus.[adrAppInfo^.id]);
		if not defined (adrMenuBar^) {
			if not dialog.confirm ("Create a new shared menu bar for " + temp.appname + "?") {
				return};
			on linkScript (scriptString) {
				local (tempscript);
				
				new (scriptType, @tempscript);
				target.set (@tempscript);
				editMenu.setFont (user.prefs.fonts.scriptfont);
				editMenu.setFontSize (user.prefs.fonts.scriptfontsize);
				op.setLineText (scriptString);
				op.insert ("Frontier.bringToFront ()", down);
				target.clear ();
				
				target.set (adrMenuBar);
				menu.setScript (@tempscript)};
			
			table.copy (@commercial.newMenuBar, @system.menus.sharedMenus);
			table.rename (@system.menus.sharedMenus.newMenuBar, adrAppInfo^.id);
			target.set (adrMenuBar);
			op.firstSummit ();
			op.expand (infinity);
			op.go (right, 1);
			op.go (down, infinity); «cursor on "Edit Scripts Table"
			linkScript ("edit (@" + temp.appname + ")");
			op.go (up, 1); «cursor on "Edit Shared Menu"
			linkScript ("edit (@" + adrMenuBar + ")");
			op.firstSummit ();
			target.clear ();
			somethingCreated = true}};
	
	if somethingCreated {
		fileMenu.save ()}}


