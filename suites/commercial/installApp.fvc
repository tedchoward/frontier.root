FrontierVcsFile:3:scpt:suites.commercial.installApp

on installApp (path) { «copies resources from a Frontier install file
	«there are two reasons why we just call the installer instead of replicating code:
		«1. it allows a user to customize the installation process
			«change your copy of commercial.installer to change the behavior
		«2. it saves space in the file format
			«every little bit counts!
	local (appname, appcreator);
	local (resdata);
	
	Frontier.findertofront = false;
	
	bundle { «get app name and creator
		rez.getstringresource (path, 140, @appname);
		rez.getstringresource (path, 141, @appcreator)};
	
	bundle { «load the app's table
		if rez.getresource (path, tabletype, 128, @resdata) {
			local (cellname = @system.verbs.apps.[appname]);
			if not dialog.ask ("Name for new scripts table?", @cellname) {
				return (false)};
			cellname = address (cellname);
			if defined (cellname^) {
				if not dialog.confirm ("Replace existing scripts table named " +  cellname + "?") {
					return (false)}};
			unpack (@resdata, cellname)}};
	
	bundle { «load the app's menubar
		if rez.getresource (path, menubartype, 128, @resdata) {
			local (cellname = @system.menus.sharedMenus.[appcreator]);
			if not dialog.ask ("Name for new shared menu?", @cellname) {
				return (false)};
			cellname = address (cellname);
			if defined (cellname^) {
				if not dialog.confirm ("Replace existing shared menu named " +  cellname + "?") {
					return (false)}};
			unpack (@resdata, cellname)}};
	
	return (true)}


