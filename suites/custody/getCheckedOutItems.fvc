FrontierVcsFile:3:scpt:suites.custody.getCheckedOutItems

on getCheckedOutItems () {
	«Return a list of records of checked out items.
		«Format:
			«"user": [current owner name], "checkedOutDate": [when the object was checked out], "address": [address of the object]
		«Wed, Jun 10, 1998 at 5:44:51 PM by PBS
	
	local (adrTable = @user.custody.objects);
	local (i, objectsList = {});
	
	custody.lockObjectsTable (); //get a semaphore lock on the objects table
	custody.cleanUpObjectsTable (); //clean up timed-out items
	
	bundle { //iterate through the objects in the table
		for i = 1 to sizeOf (adrTable^) {
			local (adrItem = @adrTable^ [i]);
			local (checkedOutDate = adrItem^.checkedOutDate);
			local (currentOwner = adrItem^.user);
			local (adrObject = nameOf (adrItem^));
			objectsList = objectsList + {{"user" : currentOwner, "checkedOutDate" : checkedOutDate, "address" : adrObject}}}};
	
	custody.unlockObjectsTable (); //unlock the semaphore
	
	return (objectsList)}


