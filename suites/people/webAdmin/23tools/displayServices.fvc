FrontierVcsFile:3:scpt:suites.people.webAdmin.["#tools"].displayServices

on displayServices () {
	local (i, servicesList);
	local (htmlText);
	local (rowBgColor = html.getPref ("rowBgColor"));
	local (altRowBgColor = html.getPref ("altRowBgColor"));
	on add (s) {
		htmlText = htmlText + s};
	on tr () {
		add ("\r\t<tr>")};
	on td () {
		local (bgcolor = "FFFFFF");
		if mod (i, 2) != 0 {
			bgcolor = rowBgColor}
		else {
			if i != 0 {
				bgcolor = altRowBgColor}};
		return (add ("\r\t\t<td align=left valign=top bgcolor=\"#" + bgcolor + "\">"))};
	on closetr () {
		add ("\r\t</tr>")};
	on closetd () {
		add ("\r\t\t</td>")};
	servicesList = people.getServicesList ();
	if not servicesList or servicesList == {} {
		add ("There are no registered services.")}
	else {
		add ("<table border=0 cellpadding=8 cellspacing=0>");
		for i = 1 to sizeOf (servicesList) {
			local (serviceName = servicesList [i]);
			local (encodedServiceName = string.urlEncode (serviceName));
			tr ();
			td ();
			add ("<a href=\"viewService.wsf?" + encodedServiceName + "\">" + serviceName + "</a>");
			closetd ();
			td ();
			add ("<a href=\"deleteService.wsf?" + encodedServiceName + "\">Delete</a>");
			closetd ();
			closetr ()};
		add ("</table>")};
	return (htmlText)}


