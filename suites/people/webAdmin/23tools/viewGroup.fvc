FrontierVcsFile:3:scpt:suites.people.webAdmin.["#tools"].viewGroup

on viewGroup () {
	local (groupName, htmlText);
	local (membersList, usersList);
	
	on add (s) {
		htmlText = htmlText + s};
	on hr () {
		add ("\r<p>\r<hr noshade size=1>")};
	
	bundle { //get group name
		groupName = html.getPageTableAddress ()^.tools^.getArgs ();
		groupName = string.urlDecode (groupName);
		if groupName == "" or groupName == false {
			return ("Can't view a group because no group name was specified.")}};
	
	«hr ()
	
	add ("<h3>Members of group " + groupName + ":</h3>\r");
	add (html.getPageTableAddress ()^.tools^.displayUsers (groupName:groupName));
	
	
	bundle { //add user to group dialog
		local (oneUser, addList = {});
		membersList = people.getGroupMembers (groupName);
		usersList = people.getUsersList ();
		
		for oneUser in usersList {
			if membersList contains oneUser {
				continue};
			addList = addList + oneUser};
		
		if addList != {} {
			hr ();
			add ("<h3>Add User to " + groupName + ":</h3>\r");
			add ("<b>Choose user:</b>\r");
			add ("<form method=get action=\"addUserToGroup.wsf\">\r");
			add ("<select name=userName>\r");
			for oneUser in addList {
				add ("<option value=\"" + oneUser + "\">" + oneUser + "\r")};
			add ("</select>\r");
			add ("<input type=\"hidden\" value=\"" + groupName + "\" name=\"groupName\">\r");
			add ("<p>\r<input type=\"submit\" value=\"Add User to Group\"></form>\r")}};
	
	hr ();
	
	add ("<h3>Services attached to group " + groupName + ":</h3>\r");
	add (html.getPageTableAddress ()^.tools^.displayGroupServices (groupName));
	
	«hr ()
	
	bundle { //attach service to group dialog
		local (servicesList = people.getGroupServices (groupName));
		if servicesList == false {
			servicesList = {}};
		local (sizeServicesList = sizeOf (servicesList));
		local (allServicesList = people.getServicesList ());
		if allServicesList == false or allServicesList == {} {
			allServicesList = {}};
		local (sizeAllServicesList = sizeOf (allServicesList));
		if sizeAllServicesList > sizeServicesList {
			if allServicesList != false and allServicesList != {} {
				hr ();
				add ("<h3>Attach service to " + groupName + ":</h3>");
				add ("<b>Choose a service:</b><p>\r");
				add ("<form method=get action=\"attachServiceToGroup.wsf\">\r");
				add ("<select name=serviceName>\r");
				for i = 1 to sizeAllServicesList {
					local (flShow = true);
					local (oneService = allServicesList [i]);
					local (j);
					for j = 1 to sizeServicesList {
						local (oneGroupService = servicesList [j]);
						if string.lower (oneGroupService) == string.lower (oneService) {
							flShow = false;
							break}};
					if flShow {
						add ("<option value=\"" + oneService + "\">" + oneService + "\r")}};
				add ("</select>\r");
				add ("<input type=\"hidden\" value=\"" + groupName + "\" name=\"groupName\">");
				add ("<p>\r<input type=\"submit\" value=\"Attach Service\"></form>")}}};
	
	«hr ()
	
	return (htmlText)}


