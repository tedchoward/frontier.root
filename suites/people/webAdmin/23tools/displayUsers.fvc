FrontierVcsFile:3:scpt:suites.people.webAdmin.["#tools"].displayUsers

on displayUsers (groupName=nil, flDeleteOption=true) {
	local (i, usersList);
	local (htmlText);
	local (rowBgColor = html.getPref ("rowBgColor"));
	local (altRowBgColor = html.getPref ("altRowBgColor"));
	on add (s) {
		htmlText = htmlText + s};
	on tr () {
		add ("\r\t<tr>")};
	on td () {
		local (bgcolor = "FFFFFF");
		if mod (i, 2) != 0 {
			bgcolor = rowBgColor}
		else {
			if i != 0 {
				bgcolor = altRowBgColor}};
		return (add ("\r\t\t<td align=left valign=top bgcolor=\"#" + bgcolor + "\">"))};
	on closetr () {
		add ("\r\t</tr>")};
	on closetd () {
		add ("\r\t\t</td>")};
	if groupName == nil {
		usersList = people.getUsersList ();
		if usersList == {} or usersList == false {
			return ("This system has no registered users.")}}
	else {
		usersList = people.getGroupMembers (groupName);
		if usersList == {} or usersList == false {
			return ("The group \"" + groupName + "\" has no members.")}};
	add ("<table border=0 cellpadding=8 cellspacing=0>");
	local (encodedGroupName);
	if groupName != nil {
		encodedGroupName = string.urlEncode (groupName)};
	for i = 1 to sizeOf (usersList) {
		local (userName = usersList [i]);
		local (encodedUserName = string.urlEncode (userName));
		tr ();
		td ();
		add ("<a href=\"viewUser.wsf?" + encodedUserName + "\">" + userName + "</a>");
		closetd ();
		if flDeleteOption {
			td ();
			if groupName != nil {
				add ("<a href=\"removeUserFromGroup.wsf?userName=" + encodedUserName + "&groupName=" + encodedGroupName + "\">Remove from group</a>")}
			else {
				add ("<a href=\"deleteUser.wsf?" + encodedUserName + "\">Delete</a>")};
			closetd ()};
		closetr ()};
	add ("</table>");
	return (htmlText)}


