FrontierVcsFile:3:scpt:suites.people.webAdmin.["#tools"].viewUser

on viewUser () {
	local (userName);
	local (i, flHidePasswords = html.getPref ("hidePasswords"));
	local (servicesList);
	local (htmlText);
	local (rowBgColor = html.getPref ("rowBgColor"));
	local (altRowBgColor = html.getPref ("altRowBgColor"));
	local (emailAddress, password);
	local (encodedUserName);
	
	on add (s) {
		htmlText = htmlText + s};
	
	bundle { //get the username
		if string.lower (html.getPageTableAddress ()^.request) beginsWith "post" { //POST request
			local (argTable);
			new (tableType, @argTable);
			webserver.parseArgs (html.getPageTableAddress ()^.requestBody, @argTable);
			temp.argTable = argTable;
			userName = argTable.oneUserName;
			emailAddress = argTable.emailAddress;
			password = argTable.password;
			if password == string.filledString ("*", sizeOf (password)) {
				password = nil}
			else {
				people.setUserPassword (userName, password)};
			people.setUserEmailAddress (userName, emailAddress)}
		else { //GET request
			userName = html.getPageTableAddress ()^.tools^.getArgs ();
			userName = string.urlDecode (userName);
			if userName == "" {
				return ("Can't display a user because no userName was specified.")}}};
	
	on tr () {
		add ("\r\t<tr>")};
	on td () {
		local (bgcolor = html.getPref ("bgColor"));
		if mod (i, 2) != 0 {
			bgcolor = rowBgColor}
		else {
			if i != 0 {
				bgcolor = altRowBgColor}};
		return (add ("\r\t\t<td align=left valign=top bgcolor=\"#" + bgcolor + "\">"))};
	on closetr () {
		add ("\r\t</tr>")};
	on closetd () {
		add ("\r\t\t</td>")};
	on header (headerText) {
		add ("<b>" + headerText + "</b>")};
	on hr () {
		add ("<p><hr noshade size=1>")};
	
	encodedUserName = string.urlEncode (userName);
	
	bundle { //add username and password
		«hr ()
		«add ("<h3>User Information:</h3>\r")
		add ("<form method=post action=\"viewUser.wsf?" + encodedUserName + "\">");
		i = 1;
		add ("<table border=0 cellpadding=8 cellspacing=0>");
		tr ();
		td ();
		header ("Username:");
		closetd ();
		td ();
		add (userName);
		add ("<input type=hidden name=oneUserName value=\"" + userName + "\">");
		closetd ();
		closetr ();
		i = 2;
		tr ();
		td ();
		header ("Password:");
		closetd ();
		td ();
		if password == nil {
			password = people.getUserPassword (userName)};
		if not flHidePasswords {
			add ("<input size=30 name=password value=\"" + password + "\">")}
		else {
			add ("<input size=30 name=password value=\"" + string.filledString ("*", sizeOf (password)) + "\">")};
		closetd ();
		closetr ();
		tr ();
		i = 3;
		td ();
		header ("Email Address:");
		closetd ();
		td ();
		if emailAddress == nil {
			emailAddress = people.getUserEmailAddress (userName)};
		«local (emailLink = "<a href=\"mailto:" + emailAddress + "\">" + emailAddress + "</a>")
		«add (emailLink)
		add ("<input size=30 name=emailAddress value=\"" + emailAddress + "\">");
		closetd ();
		closetr ();
		add ("</table>");
		add ("\r<p>\r<input type=submit value=\"Change\">");
		add ("\r</form>")};
	
	local (userGroupsList = people.getUserGroups (userName));
	if userGroupsList == false {
		userGroupsList = {}};
	local (sizeUserGroupsList = sizeOf (userGroupsList));
	
	bundle { //add list of groups
		hr ();
		header ("<h3>Groups:</h3>");
		add ("<table border=0 cellpadding=8 cellspacing=0>");
		if sizeUserGroupsList > 0 {
			for i = 1 to sizeUserGroupsList {
				tr ();
				td ();
				local (oneGroup = userGroupsList [i]);
				local (encodedGroupName = string.urlEncode (oneGroup));
				add ("<a href=\"viewGroup.wsf?" + encodedGroupName + "\">" + oneGroup + "</a>");
				closetd ();
				td ();
				add ("<a href=\"removeUserFromGroup.wsf?userName=" + encodedUserName + "&groupName=" + encodedGroupName +"\">Remove from group</a>");
				closetd ();
				closetr ()}}
		else {
			tr ();
			i = 1; //force cell background color
			td ();
			add (username + " is not a member of any groups.");
			closetd ();
			closetr ()};
		add ("</table>")};
	
	bundle { //add user to a group dialog
		local (groupsList = people.getGroupsList ());
		if groupsList != false and groupsList != {} {
			local (sizeGroupsList = sizeOf (groupsList));
			if sizeUserGroupsList < sizeGroupsList {
				hr ();
				add ("<h3>Add " + userName + " to a group:</h3>");
				add ("<b>Choose a group:</b><p>\r");
				add ("<form method=get action=\"addUserToGroup.wsf\">\r");
				add ("<select name=groupName>\r");
				for i = 1 to sizeGroupsList {
					local (flShow = true);
					local (oneGroup = groupsList [i]);
					local (j);
					for j = 1 to sizeUserGroupsList {
						local (oneUserGroup = userGroupsList [j]);
						if string.lower (oneUserGroup) == string.lower (oneGroup) {
							flShow = false;
							break}};
					if flShow {
						add ("<option value=\"" + string.urlEncode (oneGroup) + "\">" + oneGroup + "\r")}};
				add ("</select>\r");
				add ("<input type=\"hidden\" value=\"" + encodedUserName + "\" name=\"userName\">");
				add ("<p>\r<input type=\"submit\" value=\"Add To Group\"></form>")}}};
	
	«bundle //remove user from a group dialog
		«if sizeUserGroupsList > 0
			«hr ()
			«add ("<h3>Remove " + userName + " from a group:</h3>")
			«add ("<b>Choose a group:</b><p>\r")
			«add ("<form method=get action=\"removeUserFromGroup.wsf\">\r")
			«add ("<select name=groupName>\r")
			«for i = 1 to sizeUserGroupsList
				«local (oneUserGroup = userGroupsList [i])
				«add ("<option value=\"" + string.urlEncode (oneUserGroup) + "\">" + oneUserGroup + "\r")
			«add ("</select>\r")
			«add ("<input type=\"hidden\" value=\"" + encodedUserName + "\" name=\"userName\">")
			«add ("<p>\r<input type=\"submit\" value=\"Remove From Group\"></form>")
	
	servicesList = people.getUserServices (userName);
	if servicesList == false {
		servicesList = {}};
	local (sizeServicesList = sizeOf (servicesList));
	
	bundle { //add list of users services
		hr ();
		header ("<h3>Services:</h3>\r");
		add ("Note: Listed here are services attached to this user.");
		add ("The user may have access to additional services via any groups the user belongs to.<p>\r");
		add ("<table border=0 cellpadding=8 cellspacing=0>");
		if sizeServicesList > 0 {
			for i = 1 to sizeServicesList {
				tr ();
				td ();
				local (oneService = servicesList [i]);
				local (encodedServiceName = string.urlEncode (oneService));
				add ("<a href=\"viewService.wsf?" + encodedServiceName + "\">" + oneService + "</a>");
				closetd ();
				td ();
				add ("<a href=\"removeUserFromService.wsf?userName=" + encodedUserName + "&serviceName=" + encodedServiceName + "\">Remove from service</a>");
				closetd ();
				closetr ()}}
		else {
			tr ();
			i = 1; //force cell background color
			td ();
			add (username + " does not have any attached services.");
			closetd ();
			closetr ()};
		add ("</table>")};
	
	bundle { //attach service to user dialog
		local (allServicesList = people.getServicesList ());
		if allServicesList == false or allServicesList == {} {
			allServicesList = {}};
		local (sizeAllServicesList = sizeOf (allServicesList));
		if sizeAllServicesList > sizeServicesList {
			if allServicesList != false and allServicesList != {} {
				hr ();
				add ("<h3>Attach service to " + userName + ":</h3>");
				add ("<b>Choose a service:</b><p>\r");
				add ("<form method=get action=\"attachServiceToUser.wsf\">\r");
				add ("<select name=serviceName>\r");
				for i = 1 to sizeAllServicesList {
					local (flShow = true);
					local (oneService = allServicesList [i]);
					local (j);
					for j = 1 to sizeServicesList {
						local (oneUserService = servicesList [j]);
						if string.lower (oneUserService) == string.lower (oneService) {
							flShow = false;
							break}};
					if flShow {
						add ("<option value=\"" + oneService + "\">" + oneService + "\r")}};
				add ("</select>\r");
				add ("<input type=\"hidden\" value=\"" + encodedUserName + "\" name=\"userName\">");
				add ("<p>\r<input type=\"submit\" value=\"Attach Service\"></form>")}}};
	
	«hr ()
	
	return (htmlText)}


