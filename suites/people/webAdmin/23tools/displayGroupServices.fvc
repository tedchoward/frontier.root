FrontierVcsFile:3:scpt:suites.people.webAdmin.["#tools"].displayGroupServices

on displayGroupServices (groupName) {
	local (serviceList = people.getGroupServices (groupName));
	local (htmlText, i);
	
	on add (s) {
		htmlText = htmlText + s};
	on tr () {
		add ("\r\t<tr>")};
	on td () {
		local (bgcolor = "FFFFFF");
		if mod (i, 2) != 0 {
			bgcolor = rowBgColor}
		else {
			if i != 0 {
				bgcolor = altRowBgColor}};
		return (add ("\r\t\t<td align=left valign=top bgcolor=\"#" + bgcolor + "\">"))};
	on closetr () {
		add ("\r\t</tr>")};
	on closetd () {
		add ("\r\t\t</td>")};
	
	if serviceList == false or serviceList == {} {
		return ("The group \"" + groupName + "\" has no attached services.")};
	
	add ("<table border=0 cellpadding=8 cellspacing=0>");
	
	for i = 1 to sizeOf (serviceList) {
		local (serviceName = serviceList [i]);
		local (encodedserviceName = string.urlEncode (serviceName));
		local (encodedGroupName = string.urlEncode (groupName));
		tr ();
		td ();
		add ("<a href=\"viewService.wsf?" + encodedServiceName + "\">" + serviceName + "</a>");
		closetd ();
		td ();
		add ("<a href=\"removeServiceFromGroup.wsf?serviceName=" + encodedServiceName + "&groupName=" + encodedGroupName + "\">Remove service from group</a>");
		closetd ();
		closetr ()};
	
	add ("</table>");
	return (htmlText)}


