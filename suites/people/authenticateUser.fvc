FrontierVcsFile:3:scpt:suites.people.authenticateUser

on authenticateUser (userName, password, serviceName) {
	«Is this a valid user?
	«User is authenticated if...
		«1) The passwords match,
		«2) The service exists, and
		«3) The service is enabled for this user or this user's group.
	«Return the address of the data table if authentic, false otherwise.
	if not (people.checkPassword (userName, password)) {
		return (false)};
	if not (people.serviceExists (serviceName)) {
		return (false)};
	local (groupsList = people.getUserGroups (userName));
	local (i);
	for i = 1 to sizeOf (groupsList) {
		if people.serviceIsAttachedToGroup (serviceName, groupsList [i]) {
			adrDataTable = people.getGroupServiceTable (groupsList [i], serviceName);
			return (adrDataTable)}};
	if people.serviceIsAttachedToUser (serviceName, userName) {
		adrDataTable = people.getUserServiceTable (userName, serviceName);
		return (adrDataTable)};
	return (false)}


