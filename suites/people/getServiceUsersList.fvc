FrontierVcsFile:3:scpt:suites.people.getServiceUsersList

on getServiceUsersList (serviceName) {
	«Get the users of a specific service. Expand groups into individual users, remove duplicates.
		«Return false if the service doesn't exist.
	local (usersTable, usersList = {});
	if not (people.serviceExists (serviceName)) {
		return (false)};
	new (tableType, @usersTable);
	on addUserToTable (userName) {
		usersTable.[userName] = true};
	on addGroupToTable (groupName) {
		local (usersList, i);
		usersList = people.getGroupMembers (groupName);
		for i = 1 to sizeOf (usersList) {
			addUserToTable (usersList [i])}};
	people.visitServiceUsers (serviceName, @addUserToTable);
	people.visitServiceGroups (serviceName, @addGroupToTable);
	for i = 1 to sizeOf (usersTable) {
		usersList = usersList + nameOf (usersTable [i])};
	return (usersList)}


