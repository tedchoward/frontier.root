FrontierVcsFile:3:scpt:suites.mail.otherRoot.otherRootFilter

on defaultFilter (mailerappname, whatEvent, theList) {
	«this is a specialized mail filter script
		«it flows the messages out into another object db file, mail.root
		«it's created in the same folder as the Frontier app
		«if you want to try it out, copy it into user.mail.agents
	«whatEvent can take on the following values:
		«wHCn - hasConnected
		«wArv - mailArrives
		«wSnt - mailSent
		«wFil - opensFilters
		«wWcn - willConnect
	
	local (directionstring);
	case whatEvent { «we only handle in and out events
		'wHCn';
		'wFil';
		'wWcn' {
			return (true)};
		'wArv' {
			directionstring = "in"};
		'wSnt' {
			directionstring = "out"}};
	if sizeof (thelist) > 0 { «for debugging, if there's a problem we can repeat it in debugger
		scratchpad.enotlist = theList};
	local (i, mailmsg);
	mail.otherRoot.open ();
	for i = 1 to sizeOf (theList) {
		new (tabletype, @mailmsg);
		mailmsg.event = whatEvent;
		mailmsg.direction = directionstring;
		mailmsg.subject = "";
		mailmsg.date = "";
		mailmsg.recipient = "";
		mailmsg.cc = "";
		mailmsg.bcc = "";
		mailmsg.sender = "";
		mailmsg.text = "";
		mailmsg.msgnum = ++user.mail.prefs.msgcount;
		mail.drivers.[mailerappname].getmessagetable (theList [i], @mailmsg);
		if mailmsg.date == "" {
			mailmsg.date = clock.now ()};
		mail.otherRoot.addMessage (@mailmsg, user.mail.prefs.msgcount)};
	mail.otherRoot.close ();
	return (true)}
«bundle «test code
	«defaultFilter ("Eudora", 'wArv', scratchpad.enotlist)


