FrontierVcsFile:3:scpt:suites.webEdit.sendObjectToServer

on sendObjectToServer (adrObject, note="") {
	local (userName, password);
	local (server, port, paramList = {});
	local (procedureName = "webEdit.sendToServer");
	local (debug = false);
	
	local (addressString = string (adrObject));
	addressString = webEdit.fixGuestDbAdr (addressString);
	
	bundle { //get username, password, server, and port
		local (infoList = webEdit.getUserInfo (adrObject));
		if infoList == false {
			return (false)};
		userName = infoList [1];
		password = infoList [2];
		server = infoList [3];
		port = infoList [4]};
	
	bundle { //build the parameters list
		paramList [1] = username;
		paramList [2] = password;
		paramList [3] = addressstring;
		local (data = fatPages.encodePageData (adrObject));
		data = string (base64.encode (data, 0));
		paramList [4] = data;
		paramList [5] = string (typeOf (adrObject^));
		paramList [6] = note};
	
	return (webEdit.sendCommand (server, port, procedureName, @paramList, debug))}


