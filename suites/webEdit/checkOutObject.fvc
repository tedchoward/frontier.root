FrontierVcsFile:3:scpt:suites.webEdit.checkOutObject

on checkOutObject (adrObject) {
	«Changes
		«6/14/12; 9:25:47 AM by DW
			«Backup the old object locally before checking it out. 
				«http://worknotes.scripting.com/june2012/61412ByDw/makeALocalBackupOfObjectsYouCheckOut
	local (userName, password, server, port, paramList = {}, procedureName = "webEdit.checkOut", debug = false, s);
	webEdit.backupObject (adrObject); //6/14/12 by DW
	bundle { //get username, password, server, and port
		local (infoList = webEdit.getUserInfo (adrObject));
		if infoList == false {
			return (false)};
		userName = infoList [1];
		password = infoList [2];
		server = infoList [3];
		port = infoList [4]};
	bundle { //build parameters list
		paramList [1] = username;
		paramList [2] = password;
		paramList [3] = webEdit.fixGuestDbAdr (string (adrObject))};
	s = webEdit.sendCommand (server, port, procedureName, @paramList, debug);
	if typeOf (s) != stringType { //the old server returns a string
		webEdit.unpackResponse (s, adrObject)};
	return (s)};
bundle { //test code
	checkOutObject (@opmlEditor.data.outlines.prefs)}


