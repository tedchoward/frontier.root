FrontierVcsFile:3:scpt:suites.webEditServer.rpcHandlers.webEdit.checkIn

on checkIn (userName, password, adrObject, data, objectType, note="") {
	«10/17/98; 11:55:19 AM by DW
		«after the object is stored, loop over the scripts in user.webEditServer.callbacks.checkIn
		«the scripts receive four parameters:
			«adrobject -- the address of the object that was checked in
			«username -- the name of the user who checked it in
			«now -- the date/time of the change, every script gets exactly the same time
			«description -- currently webedit doesn't have support for a description, so I left room for it
	
	local (adrPreCallback = @user.webEditServer.callbacks.preCheckIn);
	local (adrCallback = @user.webEditServer.callbacks.checkIn);
	local (now = clock.now ());
	
	if not (people.authenticateUser (userName, password, "WebEdit")) {
		scriptError ("Can't check in " + adrObject + " because an invalid username or password was supplied.")};
	
	adrObject = webEditServer.fixGuestDbAdr (adrObject);
	
	«Make sure the object is checked out by this user, if the object already exists.
	if defined (adrObject^) {
		if not (custody.isCheckedOutByUser (adrObject, userName)) {
			return ("Can't check in " + adrObject + " because " + userName + " has not checked it out.")}};
	
	data = fatPages.decode (@data);
	setBinaryType (@data, string4 (objectType));
	unpack (@data, @data);
	
	webEditServer.callbackLoop (adrPreCallback, adrObject, userName, now, "");
	
	custody.checkIn (userName, password, string (address (adrObject)));
	
	table.assign (address (adrObject), data);
	
	webEditServer.callbackLoop (adrCallback, adrObject, userName, now, note);
	
	«Add to the imported objects log.
	export.addToLog (adrObject, notes:"From " + username);
	
	filemenu.saveMyRoot (adrobject);
	
	return (adrObject + " has been checked in.")}


