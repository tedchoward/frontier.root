FrontierVcsFile:3:scpt:suites.webEditServer.rpcHandlers.webEdit.delete

on delete (userName, password, adrObject) {
	«Delete an object on the server.
	
	local (adrPreCallback = @user.webEditServer.callbacks.preDelete);
	local (adrCallback = @user.webEditServer.callbacks.delete);
	local (now = clock.now ());
	
	if not (people.authenticateUser (userName, password, "WebEdit")) {
		scriptError ("Can't delete " + adrObject + " because an invalid username or password was supplied.")};
	
	adrObject = webEditServer.fixGuestDbAdr (adrObject);
	
	«Make sure the object is checked out by this user.
		«You can only delete objects you've checked out.
	if not (custody.isCheckedOutByUser (adrObject, userName)) {
		return ("Can't delete " + adrObject + " because " + userName + " has not checked it out.")};
	
	webEditServer.callbackLoop (adrPreCallback, adrObject, userName, now, "");
	
	custody.checkIn (userName, password, string (address (adrObject)));
	
	bundle { //export the object to disk before deleting it
		on cleanFileName (adr) {
			local (name = nameOf (adr^));
			local (parentName = nameOf (parentOf (adr^)^));
			local (maxlen);
			case sys.os () {
				"WinNT";
				"Win95" {
					maxlen = 250};
				"MacCn";
				"MacOS" {
					maxlen = 26}};
			if sizeOf (parentName) + sizeOf (name) + 1 < maxLen {
				name = parentName + "." + name};
			if sizeof (name) > maxlen {
				name = string.mid (name, 1, maxlen)};
			name = string.replaceAll (name, file.getPathChar (), "-");
			return (name)};
		local (day, month, year, hour, minute, second);
		date.get (clock.now (), @day, @month, @year, @hour, @minute, @second);
		local (folder = Frontier.getSubFolder ("ops/WebEdit Deleted Objects/" + year + "/" + string.padWithZeros (month, 2) + "/" + string.padWithZeros (day, 2) + "/"));
		local (suffix = "");
		if system.environment.isWindows {
			suffix = "." + Frontier.getFileSuffix (typeOf (adrObject^))};
		if system.environment.isMac and user.prefs.appendFileSuffixes {
			suffix = "." + string.lower (Frontier.getFileType (typeOf (adrObject^)))};
		local (fname = cleanFileName (adrObject) + suffix);
		local (f = folder + fname);
		
		export.sendObject (adrObject, f)}; //save the delete object to disk
	
	delete (adrObject); //delete the object
	
	webEditServer.callbackLoop (adrCallback, adrObject, userName, now, "");
	
	filemenu.saveMyRoot (adrObject);
	
	return (adrObject + " has been deleted.")}


