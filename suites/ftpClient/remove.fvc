FrontierVcsFile:3:scpt:suites.ftpClient.remove

on remove (domain, path, account, password) { « Remove remote file.
	case ftpClient.data.currentid {
		Anarchie.id {
			local (result);
			if path[sizeOf (path)] == '/' { « Anarchie doesn't like slashes at the end of directory names.
				path = string.delete (path, sizeOf (path), infinity)};
			if ftpClient.isFolder (domain, path, account, password) { « We have to recursively go through the contents
				local (i);
				for i in ftpClient.list (domain, path, account, password) {
					ftpClient.remove (domain, path + "/" + i, account, password)}};
			result = Anarchie.remove (domain, path, account, password, nil);
			if result == 0 {
				return (true)}
			else {
				Anarchie.close ();
				scriptError ("FTP Error: " + result + ".")}};
		Fetch.id {
			Fetch.remove (domain, path, account, password, nil)}}
	else {
		scriptError ("The app whose ID is '" + ftpClient.data.currentid + "' is not a supported FTP client.")}}


